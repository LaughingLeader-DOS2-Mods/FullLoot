Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
IF
DialogStarted("LLFULOOT_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
CharacterGetHostCharacter(_Host)
AND
CharacterGetReservedUserID(_Host, _UserID)
AND
CharacterGetReservedUserID(_Player, _UserID)
THEN
ObjectSetFlag(_Player, "LLFULOOT_IsHost");

IF
DialogEnded("LLFULOOT_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
THEN
ObjectClearFlag(_Player, "LLFULOOT_IsHost");
ObjectClearFlag(_Player, "LLFULOOT_CanGetNewBook");

IF
DialogStarted("LLFULOOT_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ItemTemplateIsInUserInventory(_Player, "BOOK_LLFULOOT_SettingsMenu_7056c5bd-5d3a-44c3-8511-df02f6464ccf", 0, 0)
THEN
ObjectSetFlag(_Player, "LLFULOOT_CanGetNewBook");

IF
ObjectFlagSet("LLFULOOT_AddSettingsBook", _Character, _)
THEN
ObjectClearFlag(_Character, "LLFULOOT_CanGetNewBook");
ObjectClearFlag(_Character, "LLFULOOT_AddSettingsBook");
ItemTemplateAddTo("BOOK_LLFULOOT_SettingsMenu_7056c5bd-5d3a-44c3-8511-df02f6464ccf", _Character, 1, 0);

IF
GameStarted(_Region, _)
AND
IsGameLevel(_Region, 1)
AND
GlobalGetFlag("LLFULOOT_SettingsBookAdded", 0)
AND
CharacterGetHostCharacter(_Host)
AND
ItemTemplateIsInPartyInventory(_Host, "BOOK_LLFULOOT_SettingsMenu_7056c5bd-5d3a-44c3-8511-df02f6464ccf", 0, 0)
THEN
GlobalSetFlag("LLFULOOT_SettingsBookAdded");
ItemTemplateAddTo("BOOK_LLFULOOT_SettingsMenu_7056c5bd-5d3a-44c3-8511-df02f6464ccf", _Host, 1, 0);
//Initial settings
GlobalSetFlag("LLFULOOT_AdjustGoldAmounts");
GlobalSetFlag("LLFULOOT_GoldReduction_Fourth");

IF
GameStarted(_, 1)
AND
GlobalGetFlag("LLFULOOT_SettingsBookAdded", 0)
THEN
TimerLaunch("LLFULOOT_Timers_Editor_AddSettingsBook", 250);

IF
TimerFinished("LLFULOOT_Timers_Editor_AddSettingsBook")
AND
GlobalGetFlag("LLFULOOT_SettingsBookAdded", 0)
AND
CharacterGetHostCharacter(_Host)
AND
ItemTemplateIsInPartyInventory(_Host, "BOOK_LLFULOOT_SettingsMenu_7056c5bd-5d3a-44c3-8511-df02f6464ccf", 0, 0)
THEN
GlobalSetFlag("LLFULOOT_SettingsBookAdded");
ItemTemplateAddTo("BOOK_LLFULOOT_SettingsMenu_7056c5bd-5d3a-44c3-8511-df02f6464ccf", _Host, 1, 0);

IF
CharacterUsedItemTemplate(_Player, "BOOK_LLFULOOT_SettingsMenu_7056c5bd-5d3a-44c3-8511-df02f6464ccf", _Book)
AND
QRY_SpeakerIsAvailable(_Player)
THEN
//Inventory lock workaround?
ProcObjectTimer(_Player, "LLFULOOT_Timers_OpenSettingsMenu", 100);

PROC
ProcObjectTimerFinished(_Player, "LLFULOOT_Timers_OpenSettingsMenu")
AND
QRY_SpeakerIsAvailable(_Player)
THEN
Proc_StartDialog(0, "LLFULOOT_SettingsMenu", _Player, _Player);

IF
TextEventSet("llfuloot_settings")
AND
CharacterGetHostCharacter(_Host)
AND
QRY_SpeakerIsAvailable(_Host)
THEN
Proc_StartDialog(0, "LLFULOOT_SettingsMenu", _Host, _Host);

QRY
LLFULOOT_QRY_IsIntroLevel("TUT_Tutorial_A")
THEN
DB_NOOP(1);

QRY
LLFULOOT_QRY_IsIntroLevel("FJ_FortJoy_Main")
THEN
DB_NOOP(1);

IF
GameStarted(_Region, _)
AND
IsGameLevel(_Region, 1)
AND
NOT DB_GlobalFlag("LLFULOOT_InitialSetupComplete")
AND
NOT LLFULOOT_QRY_IsIntroLevel(_Region)
THEN
TimerLaunch("LLFULOOT_Timers_StartSetupTimer", 1000);

IF
GameStarted(_Region, _)
AND
IsGameLevel(_Region, 1)
AND
NOT DB_GlobalFlag("LLFULOOT_InitialSetupComplete")
AND
LLFULOOT_QRY_IsIntroLevel(_Region)
THEN
TimerCancel("LLFULOOT_Timers_StartSetupTimer");
TimerLaunch("LLFULOOT_Timers_StartSetupTimer", 10000);

//Tutorial
PROC
Proc_TUT_StartGame()
AND
NOT DB_GlobalFlag("LLFULOOT_InitialSetupComplete")
THEN
TimerCancel("LLFULOOT_Timers_StartSetupTimer");

PROC
Proc_TUT_CheckPlayersWokenUp((CHARACTERGUID)_Player)
AND
DB_CurrentLevel("TUT_Tutorial_A")
AND
NOT DB_GlobalFlag("LLFULOOT_InitialSetupComplete")
THEN
TimerCancel("LLFULOOT_Timers_StartSetupTimer");
TimerLaunch("LLFULOOT_Timers_StartSetupTimer", 3500);

//Fort Joy
PROC
Proc_FTJ_StartGame()
AND
NOT DB_GlobalFlag("LLFULOOT_InitialSetupComplete")
THEN
TimerCancel("LLFULOOT_Timers_StartSetupTimer");

PROC
Proc_FTJ_UnfreezePlayers()
AND
DB_CurrentLevel("FJ_FortJoy_Main")
AND
NOT DB_GlobalFlag("LLFULOOT_InitialSetupComplete")
THEN
TimerCancel("LLFULOOT_Timers_StartSetupTimer");
TimerLaunch("LLFULOOT_Timers_StartSetupTimer", 3500);

IF
TimerFinished("LLFULOOT_Timers_StartSetupTimer")
AND
CharacterGetHostCharacter(_Host)
AND
QRY_SpeakerIsAvailable(_Host)
AND
NOT DB_DialogPlayers(_, _Host, _)
THEN
DB_LLFULOOT_Temp_SetupStarted(_Host);
Proc_StartDialog(0, "LLFULOOT_Setup", _Host, _Host);

//Speaker unavailable
IF
TimerFinished("LLFULOOT_Timers_StartSetupTimer")
AND
NOT DB_LLFULOOT_Temp_SetupStarted(_)
AND
NOT DB_GlobalFlag("LLFULOOT_InitialSetupComplete")
THEN
TimerLaunch("LLFULOOT_Timers_StartSetupTimer", 5000);

IF
DialogEnded("LLFULOOT_Setup", _)
AND
DB_LLFULOOT_Temp_SetupStarted(_Host)
THEN
NOT DB_LLFULOOT_Temp_SetupStarted(_Host);

//REGION AUTOMATIC_GLOBAL_FLAGS
IF
GlobalFlagSet("LLFULOOT_GoldReduction_Fourth")
AND
NOT DB_GlobalFlag("LLFULOOT_AdjustGoldAmounts")
THEN
GlobalSetFlag("LLFULOOT_AdjustGoldAmounts");

IF
GlobalFlagSet("LLFULOOT_GoldReduction_Half")
AND
NOT DB_GlobalFlag("LLFULOOT_AdjustGoldAmounts")
THEN
GlobalSetFlag("LLFULOOT_AdjustGoldAmounts");

IF
GlobalFlagCleared("LLFULOOT_GoldReduction_Fourth")
AND
NOT DB_GlobalFlag("LLFULOOT_GoldReduction_Half")
THEN
GlobalClearFlag("LLFULOOT_AdjustGoldAmounts");

IF
GlobalFlagSet("LLFULOOT_GoldReduction_Half")
AND
NOT DB_GlobalFlag("LLFULOOT_GoldReduction_Fourth")
THEN
GlobalClearFlag("LLFULOOT_AdjustGoldAmounts");

IF
GlobalFlagSet("LLFULOOT_TraderDifficulty_StrongerTraders")
AND
NOT DB_GlobalFlag("LLFULOOT_TraderDifficulty_Enabled")
THEN
GlobalSetFlag("LLFULOOT_TraderDifficulty_Enabled");

IF
GlobalFlagSet("LLFULOOT_TraderDifficulty_SpawnGuards")
AND
NOT DB_GlobalFlag("LLFULOOT_TraderDifficulty_Enabled")
THEN
GlobalSetFlag("LLFULOOT_TraderDifficulty_Enabled");

IF
GlobalFlagCleared("LLFULOOT_TraderDifficulty_StrongerTraders")
AND
NOT DB_GlobalFlag("LLFULOOT_TraderDifficulty_SpawnGuards")
THEN
GlobalClearFlag("LLFULOOT_TraderDifficulty_Enabled");

IF
GlobalFlagCleared("LLFULOOT_TraderDifficulty_SpawnGuards")
AND
NOT DB_GlobalFlag("LLFULOOT_TraderDifficulty_StrongerTraders")
THEN
GlobalClearFlag("LLFULOOT_TraderDifficulty_Enabled");
//END_REGION

//REGION DEBUG
IF
GameStarted(_, 1)
THEN
GlobalSetFlag("LLFULOOT_GoldReduction_Fourth");
GlobalSetFlag("LLFULOOT_EquippedArmorLootable");
GlobalSetFlag("LLFULOOT_EquippedWeaponsLootable");
GlobalSetFlag("LLFULOOT_TraderDifficulty_StrongerTraders");
GlobalSetFlag("LLFULOOT_TraderDifficulty_SpawnGuards");
GlobalSetFlag("LLFULOOT_InitialSetupComplete");

IF
GameStarted(_, 1)
AND
CharacterGetHostCharacter(_Host)
THEN
CharacterAddAbility(_Host, "Loremaster", 5);

IF
GameStarted("_TMPL_Sandbox", 1)
THEN
CharacterLaunchIterator("LLFULOOT_LevelUpNPC");

IF
GameStarted("_TMPL_Sandbox", 1)
AND
NOT DB_GlobalFlag("LLFULOOT_InitialSetupComplete")
THEN
TimerLaunch("LLFULOOT_Timers_StartSetupTimer", 1000);

IF
GlobalFlagSet("LLFULOOT_InitialSetupComplete")
THEN
TimerCancel("LLFULOOT_Timers_StartSetupTimer");

IF
StoryEvent((CHARACTERGUID)_NPC, "LLFULOOT_LevelUpNPC")
AND
CharacterGetHostCharacter(_Host)
AND
_NPC != _Host
AND
CharacterGetLevel(_Host, _Level)
THEN
CharacterLevelUpTo(_NPC, _Level);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_FullLoot"
